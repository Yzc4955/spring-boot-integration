plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'

    // codeGen gradle plugin
    // id 'com.codegen.gradle.plugin' version '1.0.0'
}

version '1.0.0'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // Use the awesome Spock testing and specification framework
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'

    // mybatisGen
    implementation 'org.mybatis.generator:mybatis-generator-core:1.4.0'
}

gradlePlugin {
    // Define the plugin
    plugins {
        greeting {
            id = 'com.codegen.gradle.plugin'
            implementationClass = 'com.yangzc.gradle.plugin.PluginManager'
        }
    }
}

// Add a source set for the functional test suite
sourceSets {
    functionalTest {
    }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

// Add a task to run the functional tests
tasks.register('functionalTest', Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

tasks.named('check') {
    // Run the functional tests as part of `check`
    dependsOn(tasks.functionalTest)
}

//拷贝生成的 JAR 到根目录的 lib 文件夹
task copyJarToLib(type: Copy) {
    from 'build/libs'
    into project.rootProject.file('lib')
    include '*.jar'
}

// 在 build 任务之后执行 copyJarToLib 任务
tasks.build.finalizedBy(copyJarToLib)
